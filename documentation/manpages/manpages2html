#!/usr/bin/env perl
#
# Converts fvwm manpages to html using man2html.
# Ussage: manpages2html [path]

# This entire script should really use Template::Toolkit.

use strict;
use warnings;

unless (defined $ARGV[0]) {
	die "Path to man page not given\n";
}

my @cmd_files = @ARGV;

# Index Page Header.
my $index_page = <<EOF;
---
layout : default
title : Man Pages
inner-title : Man Page Index
---

<ul>
EOF

foreach my $fpath (@cmd_files) {
	foreach (glob($fpath)) {
		unless (-e $_) {
			warn "$_ not found, skipping...\n";
			next;
		}
		build_man2html($_);
	}
}
$index_page .= "\n</ul>";

# Index Page
print "Creating Index Page.\n";
open (my $fh, ">", "index.html") or die $!;
print $fh $index_page;
close $fh;

# Build manpage with man2html
sub build_man2html
{
	my ($manpage) = @_;

	my ($title, $file, $body, $header);

	($title = $manpage) =~ s!.*/(.*)\.1$!$1!;

	# Man Page Header
	$header = <<EOF;
---
layout : default
title : Man Page - $title
inner-title : $title
---
EOF

	# Man Page Body
	$_ = `man $manpage | man2html`;

	# Trim html header and footer
	s!^.*?<body>(.*)</body>.*$!$1!si;
	s!<HR>\nThis document was created by.*$!!si;

	# Cleanup &nbsp;
	s!&nbsp;!!gi;

	# Change index links
	s!Index!Table of Contents!;
	s!Main Contents!Man Page Index!;
	s!/cgi-bin/man/man2html!index.html!;
	s!/cgi-bin/man/man2html/1\+!!g;

        $body = "<div class=\"manpage-wrapper\">\n$_\n</div>";

	$file = "$title.html";

	open my $fh, ">", $file or die $!;
	print $fh $header;
	print $fh $body;
	close $fh;

	# Add To Index page
	$index_page .= "\n<li><a href=\"${title}.html\"><b>${title}</b></a>";

        $body =~ s/.*<H2>Index<\/H2>(.*)<\/div>/$1/s;
        $body =~ s/DL/UL/g;
        $body =~ s/DT/LI/g;
        $body =~ s/DD/\/LI/g;
        $body =~ s/HREF="/HREF="$file/g;
        $index_page .= "${body}</li>";

	print "Created html manpage for: $title\n";
}
